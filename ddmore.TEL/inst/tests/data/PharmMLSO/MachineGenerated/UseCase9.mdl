# Basic Warfarin model
#
# Model as an ODE and covariate already transformed.
# IV Infusion.
#

warfarin_PK_INFUSION_dat = dataobj {
	DECLARED_VARIABLES{ CENTRAL Y }
	
	DATA_INPUT_VARIABLES {
		ID : { use=id }
		TIME : { use=idv }
		WT : { type = continuous, use=covariate }
		AMT : { use=amt, define=CENTRAL }
		RATE : { use=rate, type=continuous }  # this is how we define the rate of infusion
		DV : { use=dv, define=Y }
		logtWT:{ type = continuous }  
	} # end DATA_INPUT_VARIABLES
	
	SOURCE {
	    file = "warfarin_infusion.csv"
            inputformat = nonmemFormat 
	    ignore = "#" 
	} # end SOURCE
} # end data object


warfarin_PK_INFUSION_par = parobj {
	DECLARED_VARIABLES{ETA_CL ETA_V}

	# By default a parameter is to be estimated if fix is omitted
 	STRUCTURAL {
		POP_CL : { value = 0.1, lo = 0.001 }
		POP_V : { value = 8, lo = 0.001 } 
		# we assume that this is value, lo, high
		BETA_CL_WT : { value = 0.75, fix = true }
		BETA_V_WT : { value = 1, fix = true } 
		RUV_PROP : { value = 0.1, lo=0 }
		RUV_ADD : { value = 0.1, lo=0 } 
		} # end STRUCTURAL
	VARIABILITY {
		# Don't need to provide types. No longer needed.
		PPV_CL : { value = 0.1, type=SD }
		PPV_V : { value = 0.1, type=SD }
		OMEGA : { params=[ETA_CL, ETA_V], value = [0.01], type = CORR }
	} # end VARIABILITY
} # end of parameter object 


warfarin_PK_INFUSION_mdl = mdlobj {
	IDV { T }

	COVARIATES{
		logtWT
		}

	VARIABILITY_LEVELS{
		ID : { level=2, type=parameter  }
		DV : { level=1, type=observation }
	}

	STRUCTURAL_PARAMETERS {
		POP_CL
		POP_V
		BETA_CL_WT
		BETA_V_WT
		RUV_PROP
		RUV_ADD
	} # end STRUCTURAL_PARAMETERS
	
	VARIABILITY_PARAMETERS {
		PPV_CL
		PPV_V
	} # end VARIABILITY_PARAMETERS 
	
	RANDOM_VARIABLE_DEFINITION(level=ID) {
		ETA_CL ~ Normal(mean = 0, sd = PPV_CL)
		ETA_V ~ Normal(mean = 0, sd = PPV_V)
	} # end RANDOM_VARIABLE_DEFINITION 

	
	INDIVIDUAL_VARIABLES { # This maps to the "Type 3" individual parameter definition in PharmML
	    CL : { type = linear, trans = log, pop = POP_CL, fixEff = {coeff=BETA_CL_WT, cov=logtWT}, ranEff = ETA_CL }
	    V : { type = linear, trans = log, pop = POP_V, fixEff =  {coeff=BETA_V_WT , cov = logtWT }, ranEff = ETA_V }
	} # end INDIVIDUAL_VARIABLES

	MODEL_PREDICTION {
		DEQ{
			CENTRAL : { deriv =(- CL * CENTRAL / V), init = 0, x0 = 0 } 
		}
	    CC = CENTRAL / V 
	} # end MODEL_PREDICTION
	
	RANDOM_VARIABLE_DEFINITION(level=DV){
		EPS_Y ~ Normal(mean = 0, var = 1) # This maps the standard error model in PharmML. The error model is predefined.
	}

	OBSERVATION {
	    Y : { type = continuous, error = combinedError1(additive = RUV_ADD, proportional = RUV_PROP, f = CC),
	              eps = EPS_Y, prediction = CC } 
	} # end OBSERVATION
} # end of model object

warfarin_PK_ODE_task = taskobj {
	ESTIMATE {
	    # We need the definition of algorithms and algorithm
	    # properties before we can put anything meaningful here
	    target = MLXTRAN_CODE
	    version = "4.3.2"
	    algo = [ "SAEM" ]
	} #end ESTIMATE sub block
} # end of task object



warfarin_PK_ODE_mog = mogobj {
	OBJECTS{
		warfarin_PK_INFUSION_dat
		warfarin_PK_INFUSION_mdl
		warfarin_PK_INFUSION_par
		warfarin_PK_ODE_task
	}
}
