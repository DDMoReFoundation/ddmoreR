# Warfarin model with Between Occasion Variability (BOV)
#
# Uses ODEs. Transformation of WT covariate in COVARIATES block
# Uses "general" type (Type 2 Gaussian) relationship for individual parameters 
# 

warfarin_PK_BOV_dat = dataobj{
  DECLARED_VARIABLES{GUT Y }

  DATA_INPUT_VARIABLES{
      ID : { use=id }  
      TIME : { use=idv, units="h"}
      WT : { use=covariate, units="kg"}
      AGE : { use = covariate }
      SEX : { use=covariate, type=categorical(male, female), define=[{female, 1}, {male, 0}] }
      AMT : { use=amt, define=GUT, units="mg"}
      OCC : { use=varlevel }
      DV : { use=dv, define=Y }
      MDV : { use=mdv }
   }# end DATA_INPUT_VARIABLES

	SOURCE {
	    file = "warfarin_conc_bov_P4.csv"
        inputformat = nonmemFormat 
	    ignore = "#" 
	} # end SOURCE
} # end data object

warfarin_PK_BOV_par = parobj{
DECLARED_VARIABLES{eta_BOV_CL eta_BOV_V eta_BOV_KA eta_BOV_TLAG }
### Parameter object
# The parameter object defines the numerical values of the parameters with optional constraints (lo and high values) and units.

   STRUCTURAL{
# The STRUCTURAL block define the structural model parameters.
      #THETA
      POP_CL:{value=0.1, lo=0.001} # L/h/70kg
      POP_V:{value=8, lo=0.001} # L/70kg
      POP_KA:{value=2, lo=0.001} # h-1
      POP_TLAG:{value=1, lo=0.001} # h
	  BETA_CL_WT : { value = 0.75, fix = true }
	  BETA_V_WT : { value = 1, fix = true } 
      RUV_PROP:{value=0.1, lo=0}
      RUV_ADD:{value=0.224, lo=0} # mg/L
   }# end STRUCTURAL

   VARIABILITY{
# The VARIABILITY block has a more complex structure because it needs to express a lower triangular matrix of parameters and how they are related to each other.
# VARIABILITY parameters may be expressed with a type of "SD" which implies standard deviations on the diagonal and correlations
# on the lower off diagonal elements or "VAR" which implies variances and covariances.
      BSV_CL : { type=VAR, value=0.1 }
      BSV_V : { type=VAR, value=0.1 }

      BOV_CL : {type=VAR, value=0.1, fix=true}
      BOV_V : {type=VAR, value=0.1, fix=true}

      BSV_KA:{value=0.1, type=VAR}
      BSV_TLAG:{value=0.1, type=VAR}

      BOV_KA : {type=VAR, value=0.1}
      BOV_TLAG : {type=VAR, value=0.1}

      BOV_COV_CL_V : { type=COV, params=[eta_BOV_CL, eta_BOV_V], value=0.01} 
	  BOV_COV_KA_TLAG : { type=COV, params=[eta_BOV_KA, eta_BOV_TLAG], value=0.01 } 

   }# end VARIABILITY

} # end of parameter object

warfarin_PK_BOV_mdl = mdlobj{
   IDV { TIME }

   COVARIATES{
	WT
	logtWT = log(WT/70)
   }

   VARIABILITY_LEVELS{
	ID : { level=3, type=parameter }
	OCC : { level=2, type=parameter }
   	DV : { level=1, type=observation }
   }

   STRUCTURAL_PARAMETERS{
      #THETA
      POP_CL
      POP_V
      POP_KA
      POP_TLAG
	  BETA_CL_WT
	  BETA_V_WT
      RUV_PROP
      RUV_ADD
   }# end STRUCTURAL_PARAMETERS

   VARIABILITY_PARAMETERS{
      #OMEGA
      BSV_CL
      BSV_V
      BOV_CL
      BOV_V
      BOV_KA
      BOV_TLAG
      BSV_KA
      BSV_TLAG
   }# end VARIABILITY_PARAMETERS

	GROUP_VARIABLES{
      # Covariate model
      GRPCL=log(POP_CL) + BETA_CL_WT*logtWT  ## Type 1 - Equation type
      GRPV=log(POP_V) + BETA_V_WT*logtWT
      GRPKA=log(POP_KA)
      GRPLG=log(POP_TLAG)
   }# end GROUP_VARIABLES
   
   RANDOM_VARIABLE_DEFINITION(level=ID){
      eta_BSV_CL~ Normal(mean=0, var=BSV_CL)
      eta_BSV_V~ Normal(mean=0, var=BSV_V)
      eta_BSV_KA~ Normal(mean=0, var=BSV_KA)
      eta_BSV_TLAG~ Normal(mean=0, var=BSV_TLAG)
   }# end RANDOM_VARIABLE_DEFINITION

   RANDOM_VARIABLE_DEFINITION(level=OCC){
      eta_BOV_CL~ Normal(mean=0, var=BOV_CL)
      eta_BOV_V~ Normal(mean=0, var=BOV_V)
      eta_BOV_KA~ Normal(mean=0, var=BOV_KA)
      eta_BOV_TLAG~ Normal(mean=0, var=BOV_TLAG)
   }# end RANDOM_VARIABLE_DEFINITION

   INDIVIDUAL_VARIABLES{
      # Individual parameters
	  CL : {type=general, trans=log, pop=GRPCL, ranEff=[eta_BSV_CL,eta_BOV_CL] } ## Type 2 - Guassian with general covariate model
	  V  : {type=general, trans=log, pop=GRPV, ranEff=[eta_BSV_V,eta_BOV_V] }
      KA : {type=general, trans=log, pop=GRPKA, ranEff=[eta_BSV_KA,eta_BOV_KA] }
      TLAG : {type=general, trans=log, pop=GRPLG, ranEff=[eta_BSV_TLAG,eta_BOV_TLAG] }
   }# end INDIVIDUAL_VARIABLES

   MODEL_PREDICTION{
	    DEQ {  
			RATEIN = GUT * KA when TIME >= TLAG otherwise 0
			GUT : { deriv =(- RATEIN), init = 0, x0 = 0 }
	        CENTRAL : { deriv =(RATEIN - CL * CENTRAL / V), init = 0, x0 = 0 } 
	    } # end DEQ block
	    CC = CENTRAL / V 
   }# end MODEL_PREDICTION

	RANDOM_VARIABLE_DEFINITION(level=DV) {
		EPS_Y ~ Normal(mean = 0, var = 1) 
	} # end RANDOM_VARIABLE_DEFINITION 
	
	OBSERVATION {
	    Y : { type = continuous, error = combinedError1(additive = RUV_ADD, proportional = RUV_PROP, f = CC),
	              eps = EPS_Y, prediction = CC } 
	} # end OBSERVATION

} # end of model object

warfarin_PK_BOV_task = taskobj{

      ESTIMATE {
         algo = ["FOCEI"]
      }
}# end of taskobj

warfarin_PK_BOV_mog=mogobj{
	OBJECTS{
		warfarin_PK_BOV_mdl
		warfarin_PK_BOV_par
		warfarin_PK_BOV_dat
		warfarin_PK_BOV_task
	}
}
