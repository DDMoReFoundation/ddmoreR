# Version: 1.0.2 (alpha) Build Number: 816 Build ID: 2014-08-04_18-55-27

### Data object
Friberg_JCO2002_Myelosuppression_dat = dataobj{

   DATA_INPUT_VARIABLES{
      ID: {type=categorical}
      TIME: {type=continuous,units="h"}
      RATE: {type=continuous, units="umol/h"} # 3h infusion
      AMT: {type=continuous, units="umol"} 
      DV: {type=continuous, units="leukocytes*10^9/L"} # leukocyte count
      CMT: {type=categorical} 
      EVID: {type=categorical} 
      CLI: {type=continuous, units="L/h"} # individual estimate for the clearance
      V1I: {type=continuous, units="L"} # individual estimate for the central volume of distribution
      V2I: {type=continuous, units="L"} # individual estimate for the peripheral volume of distribution
   }# end DATA_INPUT_VARIABLES


   SOURCE{
           file="data_ddmore_WBC_pacl_mod.csv"
           inputformat=nonmemFormat
           ignore="@"
   }
} # end data object


### Parameter object
Friberg_JCO2002_Myelosuppression_par = parobj{

   STRUCTURAL{
      pop_CIRC0: {value=7.21, lo=2, hi=13, units="leukocytes*10^9/L"}
      pop_MTT: {value=124, lo=0, units="h"}
      GAMMA: {value=0.239, lo=0}
      pop_SLOPu: {value=28.9, lo=0, units="L/umol"}
      prop_error: {value=0.286, lo=0}
   }# end STRUCTURAL

   VARIABILITY{
      omega_CIRC0: {value=0.107 ,type=VAR}
      omega_MTT: {value=0.0296, type=VAR}
      omega_SLOPu: {value=0.176, type=VAR}
      
      sigma_ERROR: {value=1, type=VAR, fix=true}
   }# end VARIABILITY

} # end of parameter object

### Model object
Friberg_JCO2002_Myelosuppression_mdl = mdlobj{

   MODEL_INPUT_VARIABLES{
      ID : {type=categorical, use=id,level=2}
      TIME : {type=continuous, use=idv,units="h"}
      RATE : {type=continuous, use=rate, units="umol/h"}
      AMT : {type=continuous, use=amt, units="umol", administration=Ac}
      DV : {type=continuous,use=dv,level=1, prediction=Y, units="leukocytes*10^9/L"}
      CMT : {type=categorical, use=cmt}
      EVID : {type=categorical,use=evid}
      CLI : {type=continuous,use=covariate}
      V1I : {type=continuous,use=covariate}
      V2I : {type=continuous,use=covariate}
   }# end MODEL_INPUT_VARIABLES

   STRUCTURAL_PARAMETERS{
      pop_CIRC0      # baseline circulating leukocyte cells
      pop_MTT        # mean transit time
      GAMMA          # shape parameter of feedback
      pop_SLOPu      # linear parameter representing the unbound drug effect
      prop_error      # Typical_residual_error
   }# end STRUCTURAL_PARAMETERS

   VARIABILITY_PARAMETERS{
      omega_CIRC0
      omega_MTT
      omega_SLOPu
      sigma_ERROR
   }# end VARIABILITY_PARAMETERS


   RANDOM_VARIABLE_DEFINITION{
      ETA_CIRC0 ~ {type=normal, mean=0, var=omega_CIRC0, level=ID}
      ETA_MTT ~ {type=normal, mean=0, var=omega_MTT, level=ID}
      ETA_SLOPu ~ {type=normal, mean=0, var=omega_SLOPu, level=ID}
   }# end RANDOM_VARIABLE_DEFINITION

   INDIVIDUAL_VARIABLES{
   CIRC0 : {type=linear, trans=log, pop = pop_CIRC0, ranEff=ETA_CIRC0} 
   MTT : {type=linear, trans=log, pop = pop_MTT, ranEff=ETA_MTT} 
   SLOPu : {type=linear, trans=log, pop = pop_SLOPu, ranEff=ETA_SLOPu}
     
   NN = 3        # number of transit compartments, fixed
   KTR = (NN+1)/MTT
   

   #PK parameters
   CL = CLI
   V1 = V1I
   V2 = V2I
   Q = 204
   
   K = CL/V1
   K12 = Q/V1
   K21 = Q/V2
   }# end INDIVIDUAL_VARIABLES


   MODEL_PREDICTION{
    ODE{
        # Drug PK
        CONC = Ac/V1
        Ac : {deriv= (-K12*Ac - K*Ac + K21*Ap)}
        Ap : {deriv= (K12*Ac - K21*Ap)}
        
        # Mielosuppression model
        EDRUG= 1 - SLOPu*CONC
        FEED= (CIRC0/CIRC)^GAMMA
        
        CIRC : {deriv = (KTR*TRANSIT3 - KTR*CIRC), init=CIRC0}
        PROL : {deriv=  (KTR*PROL*EDRUG*FEED - KTR*PROL), init=CIRC0}
        TRANSIT1 : {deriv= (KTR*PROL - KTR*TRANSIT1), init=CIRC0}
        TRANSIT2 : {deriv= (KTR*TRANSIT1 - KTR*TRANSIT2), init=CIRC0}
        TRANSIT3 : {deriv= (KTR*TRANSIT2 - KTR*TRANSIT3), init=CIRC0}
    }
   }# end MODEL_PREDICTION

 
   OBSERVATION{
     eps_ERROR ~ {type=normal, mean=0, var=sigma_ERROR, level=DV}
     
     Y : {type=continuous, error=proportionalError(proportional=prop_error,f=CIRC),
      	eps=eps_ERROR, prediction=CIRC}
    
   }# end OBSERVATION

} # end of model object



Friberg_JCO2002_Myelosuppression_task = taskobj{
### Task Properties object

      ESTIMATE {
         target=NMTRAN_CODE
         cov=true
         algo=["FOCEI"] 
      }# end ESTIMATE
}# end of task object

Friberg_JCO2002_Myelosuppression = mog{
Friberg_JCO2002_Myelosuppression_mdl
Friberg_JCO2002_Myelosuppression_dat
Friberg_JCO2002_Myelosuppression_task
Friberg_JCO2002_Myelosuppression_par
}