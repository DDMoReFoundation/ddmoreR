# Del Bene 2009
# TGI model in vitro with constant concentration (CONC input) of drug

delbene2009_data = dataobj{
   DATA_INPUT_VARIABLES{
      ID: {type=categorical}
      TIME: {type=continuous,units="h"}
      DV: {type=continuous}
      CONC: {type=continuous,units="umol/L"}
      EVID: {type=categorical}
   } 
   
   SOURCE{
           file="delbene2009_data.csv"
           ignore="@"
           inputformat=nonmemFormat
           header=true
   }
}


delbene2009_par = parobj{

   STRUCTURAL{
      #PARAMETER VALUES TO USE FOR ESTIMATION:
      LAMBDA0: {value=0.1, lo=0, units="1/h"}
      K1: {value=0.1, lo=0, units="1/h"}
      K2: {value=0.1, lo=0, units="L/h/umol"}
      N0: {value=1000, lo=0}
      CV: {value=0.1, lo=0}
   }

   VARIABILITY{
      SIGMA_RES_W: {value=1 ,type=VAR, fix=true}
   }
}

delbene2009_mdl = mdlobj{

	MODEL_INPUT_VARIABLES{
	  ID: {use=id,level=2}
      TIME: {use=idv,units="h"}
      DV: {type=continuous,use=dv,level=1, prediction=Y}
      CONC: {use=covariate,type=continuous,units="umol/L"}
      EVID: {use=evid} 
	} 
	
	STRUCTURAL_PARAMETERS{
	  LAMBDA0
      K1 
      K2
      N0
      CV    
	}
	
	VARIABILITY_PARAMETERS{
      SIGMA_RES_W
	}
		


	MODEL_PREDICTION{

		ODE{
			NP: {deriv=LAMBDA0*NP-K2*CONC*NP, init=N0}
			N1: {deriv=K2*CONC*NP-K1*N1, init=0}
			N2: {deriv=K1*N1-K1*N2, init=0 }
			N3: {deriv=K1*N2-K1*N3, init=0}			
		}
		   NT=NP + N1 + N2 + N3
   }
   
   OBSERVATION{
    eps_RES_W ~ {type=normal, mean=0, var=SIGMA_RES_W, level=DV}
	  
     Y : {type=continuous, error=proportionalError(proportional=CV, f=NT),
   	eps=eps_RES_W, prediction=NT}
   }
   
   MODEL_OUTPUT_VARIABLES{ 
	ID TIME DV CONC
    }  
}

delbene2009_task = taskobj{ 

    ESTIMATE { 
    	target=NMTRAN_CODE 
        cov=true
        algo= ["FOCE"] 
	}
	
}

delbene2009 = mog{
delbene2009_data
delbene2009_task
delbene2009_mdl
delbene2009_par
}