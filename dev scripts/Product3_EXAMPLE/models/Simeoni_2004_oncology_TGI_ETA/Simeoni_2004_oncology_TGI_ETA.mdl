# Simeoni 2004
# TGI model in vivo

simeoni2004_data = dataobj{
   DATA_INPUT_VARIABLES{
      ID:{type=categorical}
      TIME:{type=continuous,units="day"}
      DV:{type=continuous}
      AMT:{type=continuous,units="mg/kg"}
      EVID:{type=categorical}
      CMT:{type=continuous}
   }
   
   SOURCE{
       file="simeoni2004_data.csv"
       inputformat=nonmemFormat
       ignore="@"
       header=true
   }
}

simeoni2004_par = parobj{

   STRUCTURAL{
      #PARAMETER VALUES TO USE FOR ESTIMATION:
      POP_LAMBDA0:{value=0.3, lo=0, units="1/day"}
      LAMBDA1:{value=0.3, lo=0, units="g/day"}
      K1:{value=0.3, lo=0, units="1/day"}
      K2:{value=0.5, lo=0, units="L/mg/day"}
      W0:{value=0.02, lo=0, units="g"}
      CV:{value=0.1, lo=0}
   }
   
   VARIABILITY{
	OMEGA_LAMBDA0:{value=0 ,type=VAR, fix=true}
  SIGMA_RES_W:{value=1 ,type=VAR, fix=true}
	}

}

simeoni2004_mdl = mdlobj{

	MODEL_INPUT_VARIABLES{
	  ID:{use=id, level=2}
      TIME:{use=idv,units="day"}
      DV:{type=continuous,use=dv,level=1, prediction=Y}
      AMT:{use=amt,units="mg/kg", administration=Q1}
      EVID:{use=evid}
      CMT:{use=cmt}
	}
	
	STRUCTURAL_PARAMETERS{
      POP_LAMBDA0
      LAMBDA1
      K1
      K2
      W0
      CV
	}
	
	VARIABILITY_PARAMETERS{
      OMEGA_LAMBDA0
      SIGMA_RES_W
	}
	
  RANDOM_VARIABLE_DEFINITION{
      ETA_OMEGA_LAMBDA0 ~ {type=normal, mean=0, var=OMEGA_LAMBDA0, level=ID}
      }	
	
	INDIVIDUAL_VARIABLES{
		K10=0.868*24 #20.832 1/day
		K12=0.006*24 #0.144 1/day
    K21=0.0838*24 #2.011 1/day
    V1=0.81 #L/kg
    PSI=20
    
    LAMBDA0 : {type=linear, trans=log, pop = POP_LAMBDA0, ranEff=ETA_OMEGA_LAMBDA0}    
   	}
	
	MODEL_PREDICTION{
      	
		ODE{
		    C=Q1/V1
			WTOT=X1+X2+X3+X4
			Q1:{deriv=K21*Q2-(K10+K12)*Q1, init=0, wrt=T}
			Q2:{deriv=K12*Q1-K21*Q2, init=0, wrt=TIME}
			X1:{deriv=(LAMBDA0*X1/((1+(WTOT*LAMBDA0/LAMBDA1)^PSI)^(1/PSI)))-K2*C*X1, init=W0, wrt=T}
			X2:{deriv=K2*C*X1-K1*X2, init=0, wrt=T}
			X3:{deriv=K1*X2-K1*X3, init=0, wrt=T}
			X4:{deriv=K1*X3-K1*X4, init=0, wrt=T}
		}
	}	
   
   OBSERVATION{
  		eps_RES_W ~ {type=normal, mean=0, var=SIGMA_RES_W, level=DV}		
 		  Y : {type=continuous, error=proportionalError(proportional=CV, f=WTOT), eps=eps_RES_W, prediction= WTOT}
   }
   
    MODEL_OUTPUT_VARIABLES{ 
	ID TIME DV AMT CMT WTOT
    }
}
   
simeoni2004_task = taskobj{ 
    ESTIMATE { 
    	target=NMTRAN_CODE 
        algo=["FOCE"] 
        cov=true
	}
}

simeoni2004 = mog{
	simeoni2004_task
	simeoni2004_mdl
	simeoni2004_par
	simeoni2004_data
}

