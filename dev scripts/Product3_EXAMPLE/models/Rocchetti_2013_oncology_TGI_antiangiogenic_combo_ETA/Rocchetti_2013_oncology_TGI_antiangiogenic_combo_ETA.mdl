# Rocchetti 2013
# TGI model in vivo with two drugs in combination

rocchetti2013_data = dataobj{
   DATA_INPUT_VARIABLES{
      ID:{type=categorical}
      TIME:{type=continuous,units="day"}
      DV:{type=continuous}
      AMT:{type=continuous,units="umol/kg"}
      CMT:{type=categorical}
      EVID:{type=categorical}
   }
   
	SOURCE{
   		file="rocchetti2013_data.csv"
    	ignore="@"
        inputformat=nonmemFormat
        header=true
   }
}

rocchetti2013_par = parobj{

   STRUCTURAL{
      #PARAMETER VALUES TO USE FOR ESTIMATION:
      K2:{value=0.221, fix=true, units="L/umol/day"}
      POP_LAMBDA0:{value=0.14, fix=true, units="1/day"}
      LAMBDA1:{value=0.129, fix=true, units="g/day"}
      K1:{value=3.54, fix=true, units="1/day"}
      IC50:{value=2.02, fix=true, units="umol/L"}
      IC50COMBO:{value=7, lo=0, units="umol/L"}
      W0:{value=0.062, fix=true, units="g"}
      CV:{value=0.1, lo=0}
   }

   VARIABILITY{
      OMEGA_LAMBDA0:{value=0 ,type=VAR, fix=true}
      SIGMA_RES_W:{value=1 ,type=VAR,fix=true}
   }
   
}


rocchetti2013_mdl = mdlobj{

	MODEL_INPUT_VARIABLES{
	  ID:{use=id, level=2}
      TIME:{use=idv,units="day"}
      DV:{type=continuous,use=dv,level=1, prediction=Y}
      AMT:{use=amt,units="umol/kg"}
      CMT:{use=cmt}
      EVID:{type=categorical, use=evid}
      }
	
	STRUCTURAL_PARAMETERS{
      K2
      POP_LAMBDA0
      LAMBDA1
      K1
      IC50
      IC50COMBO
      W0
      CV
	}
	
	VARIABILITY_PARAMETERS{
      OMEGA_LAMBDA0
      SIGMA_RES_W
	}
		
	RANDOM_VARIABLE_DEFINITION{
     ETA_OMEGA_LAMBDA0 ~ {type=normal, mean=0, var=OMEGA_LAMBDA0, level=ID}
     	}
	
	
	INDIVIDUAL_VARIABLES{
	  KA_A=24*ln(2)/6.19 #=2.69 1/day
      KE_A=ln(2)/6.05 #=0.115 1/day
      FV1_A=1/0.119 #8.4 kg/L
      KA_B=18.8 #1/day
      KE_B=49.2 #1/day
      K21=10.4 #1/day
      K12=141.1 #1/day
      FV1_B=1/2.13 #0.469 kg/L
      EMAX=1
      PSI=20
      
      LAMBDA0 : {type=linear, trans=log, pop = POP_LAMBDA0, ranEff=ETA_OMEGA_LAMBDA0} 
	}
	
	MODEL_PREDICTION{

	    ODE{
		C1_A=Q1_A*FV1_A
		C1_B=Q1_B*FV1_B
		K2INH=K2*(1-C1_A/(C1_A+IC50COMBO))
		WTOT=Z0+Z1+Z2+Z3
		Q0_A:{deriv=-KA_A*Q0_A, init=0, wrt=T}
		Q1_A:{deriv=KA_A*Q0_A-KE_A*Q1_A, init=0, wrt=T}
		Q0_B:{deriv=-KA_B*Q0_B, init=0, wrt=TIME}
		Q1_B:{deriv=KA_B*Q0_B-(K12+KE_B)*Q1_B+K21*Q2_B, init=0, wrt=T}
		Q2_B:{deriv=K12*Q1_B-K21*Q2_B, init=0, wrt=T}
		Z0:{deriv=(LAMBDA0*Z0/((1+(WTOT*LAMBDA0/LAMBDA1)^PSI)^(1/PSI)))*(1-EMAX*C1_A/(C1_A+IC50))-K2INH*C1_B*Z0, init=W0, wrt=T}
		Z1:{deriv=K2INH*C1_B*Z0-K1*Z1, init=0, wrt=T}
		Z2:{deriv=K1*Z1-K1*Z2, init=0, wrt=T}
		Z3:{deriv=K1*Z2-K1*Z3, init=0, wrt=T}
	    }
   
   }
   
   OBSERVATION{
     # This could go in the RANDOM VARIABLES section but it refers to the OBS model so it can be put here too. Makes it clearer that
     # this describes the residual error.
     eps_RES_W ~ {type=normal, mean=0, var=SIGMA_RES_W, level=DV}
     #Y =  WTOT + WTOT*CV*eps_RES_W
     Y : {type=continuous, error=proportionalError(proportional=CV, f=WTOT),
     eps=eps_RES_W, prediction=WTOT}    
  
   }
	
	MODEL_OUTPUT_VARIABLES{ 
	ID TIME DV AMT CMT EVID
    }
}

rocchetti2013_task = taskobj{ 
    MODEL{ 
        tolrel=6
    } 
  
    ESTIMATE { 
    	target = NMTRAN_CODE 
    	algo = [ "FOCE" ] 
    	cov = true 
    }
    
}

# Strictly this is not part of MCL so I have commented it out although I know Natalia's parser does recognise it.
rocchetti2013 = mog{
	rocchetti2013_mdl
	rocchetti2013_task
	rocchetti2013_par
	rocchetti2013_data
}