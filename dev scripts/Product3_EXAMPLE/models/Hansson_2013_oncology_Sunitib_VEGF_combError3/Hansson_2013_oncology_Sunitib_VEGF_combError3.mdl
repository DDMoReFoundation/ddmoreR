# Model based on the Hansson et al. CPT:PsP 2013
# PKPD modeling of VEGF, sVEGFR-2, sVEGFR-3 and sKIT following sunitinib treatment in GIST


Hansson2013_data = dataobj{
   DATA_INPUT_VARIABLES{
      ID:{type=categorical}
      CYCL:{type=categorical}
      TIME:{type=continuous,units="h"}
      DAYS:{type=continuous,units="DAYS"}
      FLAG:{type=categorical} # 1=Dose ; 4=Tumor size (SLD); 5=VEGF 
      DVX:{type=continuous, units="mg/L"}
      DV:{type=continuous,units="mg/L"} # log transform data
      DOS:{type=continuous,units="mg"}
      PLA:{type=categorical} # placebo=1, treated=0
      CL:{type=continuous,units="L/h"}
      EVID:{type=categorical}
   }
   
   SOURCE{
       file="simVEGF.csv"
       inputformat=nonmemFormat
       ignore="#"
   }
}

Hansson2013_par = parobj{

   STRUCTURAL{

      POP_IMAX:{value=1, fix=true} 
      POP_IC50:{value=1, lo=0, units="mg/L*h"} 
      
      POP_BM0:{value=59.7, lo=0, units="pg/mL"} # baseline VEGF
      POP_MRT:{value=91, lo=0, units="h"} 
      POP_HILL:{value=3.31, lo=0}
      POP_TVSLP:{value=0.035, lo=-0.06, units="1/h"}
                
      #Residual Error
      POP_RES_VEGF_PROP:{value=0.445, lo=0} 
      POP_RES_VEGF_ADD:{value=0, fix=true} 
   }

   VARIABILITY{
     OMEGA_BM0:{value=0.252,type=VAR}     
     OMEGA_MRT_VEGFs:{value=0.060, type=VAR}     
     OMEGA_IC50 : { value=0.253, type=VAR }
     OMEGA_TVSLP:{value=2.95, type=VAR}
     SIGMA_RES_W:{value=1 ,type=VAR, fix=true}
   }
}

### Model object
Hansson2013_mdl = mdlobj{

   MODEL_INPUT_VARIABLES{
      ID:{type=categorical, use=id,level=2}
      CYCL:{type=categorical}
      TIME:{type=continuous, use=idv, units="h"}
      DAYS:{type=continuous,units="DAYS"}
      DVX:{type=continuous, units="mg/L"} 
      DV:{type=continuous,units="mg/L",use=dv, prediction=VEGF_obs, level=1}
      DOS:{type=continuous,units="mg"}
      CL:{type=continuous,units="L/h", use=covariate}
      EVID:{type=categorical, use=evid}
   }# end MODEL_INPUT_VARIABLES

   STRUCTURAL_PARAMETERS{
      POP_IMAX
      POP_IC50 # Common IC50 for the four biomarkers which were found to be highly correlated

      #VEGF
      POP_BM0  # baseline VEGF
      POP_MRT 
      POP_HILL
      POP_TVSLP
      
      #Residual Error
      POP_RES_VEGF_PROP
      POP_RES_VEGF_ADD 
           
   }# end STRUCTURAL_PARAMETERS

   VARIABILITY_PARAMETERS{
      OMEGA_BM0      
      OMEGA_MRT_VEGFs      
      OMEGA_IC50              
      OMEGA_TVSLP
      SIGMA_RES_W
   }# end VARIABILITY_PARAMETERS


   RANDOM_VARIABLE_DEFINITION{
      ETA_BM0 ~ {type=normal, mean=0, var=OMEGA_BM0, level=ID}  
      ETA_MRT_VEGFs ~ {type=normal, mean=0, var=OMEGA_MRT_VEGFs, level=ID} 
      ETA_IC50 ~ {type=normal, mean=0, var=OMEGA_IC50, level=ID}    
      ETA_TVSLP ~ { type=normal, mean=0, var=OMEGA_TVSLP, level=ID}
 
}# end RANDOM_VARIABLE_DEFINITION

   INDIVIDUAL_VARIABLES{
      BM0 : {type=linear, trans=log, pop = POP_BM0, ranEff=ETA_BM0}                 
      IMAX1 = POP_IMAX    
      IC50 : {type=linear, trans=log, pop = POP_IC50, ranEff=ETA_IC50} 
      HILL = POP_HILL     
      MRT1 : {type=linear, trans=log, pop = POP_MRT, ranEff=ETA_MRT_VEGFs}      
      TVSLP = POP_TVSLP/1000
      DPSLP : {type=linear, trans=log, pop = TVSLP, ranEff=ETA_TVSLP} 
                  
      # Derived parameters
      KOUT = 1/MRT1
    
      }# end INDIVIDUAL_VARIABLES


   MODEL_PREDICTION{
      AUC = DOS/CL

      DP1 = BM0*(1+DPSLP*TIME)        # Linear disease progression model for VEGF
      KIN = DP1*KOUT
     
       ODE{
       EFF = IMAX1*AUC^HILL /(IC50^HILL+AUC^HILL)     # VEGF  : sigmoid Imax drug effect relationship
      
       VEGF : {deriv=KIN -KOUT*(1-EFF)*VEGF, init=BM0}
    }
   }# end MODEL_PREDICTION

 
   OBSERVATION{
      eps_RES_W ~ {type=normal, mean=0, var=SIGMA_RES_W, level=DV}
                      
   	  VEGF_obs : {type=continuous, error=combinedError2log(additive=POP_RES_VEGF_ADD, proportional=POP_RES_VEGF_PROP), prediction=VEGF, eps=eps_RES_W}
      

   }# end OBSERVATION

} # end of model object
       
       
Hansson2013_task = taskobj{
### Task Properties object

    ESTIMATE { 
    	target=NMTRAN_CODE 
        cov=true
        algo= ["FOCEI"] 
	}
}# end of task object

Hansson2013 = mog{
	Hansson2013_data
	Hansson2013_par
	Hansson2013_mdl
	Hansson2013_task
}