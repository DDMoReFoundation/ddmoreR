warfarin_PKPD_turnover_dat = dataObj{
     DECLARED_VARIABLES{CP_obs; PCA_obs; GUT }

     DATA_INPUT_VARIABLES{
      ID : 	 { use is id }
      TIME : { use is idv }
      WT : 	 { use is covariate }
      AGE :  { use is covariate }
      SEX :  { use is catCov withCategories{female when 0, male when 1} }
      AMT :  { use is amt, variable = GUT }
      DVID : { use is dvid }
      DV :   { use is dv, define={1 in DVID as CP_obs, 2 in DVID as PCA_obs} }
      MDV :  { use is mdv }
   }# end HEADER

   SOURCE{
      srcfile : {file="warfarin_conc_pca.csv", 
      			 inputFormat is nonmemFormat }
   }# end FILE
} # end data object

warfarin_PKPD_turnover_par = parObj{
	DECLARED_VARIABLES{ETA_CL ETA_V}
	
   STRUCTURAL{
# The STRUCTURAL block define the structural model parameters.
      #THETA
      POP_CL : {value=0.1,lo=0.01,hi=1} # L/h/70kg
      POP_V : {value=8,lo=0.01,hi=20} # L/70kg
      POP_KA : {value=0.362,lo=0.01,hi=24} # h
      POP_TLAG : {value=1,lo=0.01,hi=24} # h
      POP_PCA0 : {value=96.7,lo=0.01,hi=200}
      POP_EMAX : {value=1, lo=0, fix=true}
      POP_C50 : {value=1.2,lo=0.01,hi=10}
      POP_TEQ : {value=12.9,lo=0.01,hi=100}
	  BETA_CL_WT : { value = 0.75, fix = true }
	  BETA_V_WT : { value = 1, fix = true }
      #SIGMA
      RUV_PROP : {value=0.05, lo=0 }
      RUV_ADD : {value=0.3, lo=0 } # mg/L
      RUV_FX : {value=4, lo=0 }
   }# end STRUCTURAL

   VARIABILITY{
      #OMEGA
      PPV_CL : {value=0.1 ,type is sd}
      PPV_V : {value=0.1 ,type is sd}
      PPV_KA : {value=0.1 ,type is sd}
      PPV_TLAG : {value=0.1 ,type is sd, fix=true}
      PPV_PCA0 : {value=0.1 ,type is sd}
      PPV_EMAX : {value=0 ,type is sd,fix=true}
      PPV_C50 : {value=0.1 ,type is sd}
      PPV_TEQ : {value=0.1 ,type is sd}
      ## Correlation
      OMEGA : { parameter=[ETA_CL, ETA_V], value = [0.01], type is corr }
   }# end VARIABILITY

} # end of parameter object

warfarin_PKPD_turnover_mdl = mdlObj{
	IDV { T }

	VARIABILITY_LEVELS{
		ID : { level=2, type is parameter }
		DV : { level=1, type is observation }	
	}

	COVARIATES{
		WT
		logtWT = ln(WT/70)
	}
	
# The STRUCTURAL_PARAMETERS Block defines the structural model parameters.
   STRUCTURAL_PARAMETERS{
# A name for each parameter is required. Other attributes such as units are optional.
# Units may be used to achieve consistency between the parameter, data and model object values.
# This could be implemented during the WP2 translation process.

      #THETA
      POP_CL
      POP_V
      POP_KA
      POP_TLAG
      POP_PCA0
      POP_EMAX
      POP_C50
      POP_TEQ
      BETA_CL_WT
      BETA_V_WT
      #SIGMA
      RUV_PROP
      RUV_ADD
      RUV_FX
    }# end STRUCTURAL_PARAMETERS

   VARIABILITY_PARAMETERS{
# The VARIABILITY_PARAMETERS Block defines the variability parameters.
      #OMEGA
      PPV_CL
      PPV_V
      PPV_KA
      PPV_TLAG
      PPV_PCA0
      PPV_EMAX
      PPV_C50
      PPV_TEQ
   }# end VARIABILITY_PARAMETERS

   RANDOM_VARIABLE_DEFINITION(level=ID){
# The RANDOM_VARIABLE_DEFINITION block is used to define the distribution of random variables.
      ETA_CL ~ Normal( mean=0, sd=PPV_CL) 
      ETA_V ~ Normal( mean=0, sd=PPV_V)
      ETA_KA ~ Normal( mean=0, sd=PPV_KA)
      ETA_TLAG ~ Normal( mean=0, sd=PPV_TLAG)
      ETA_PCA0 ~ Normal( mean=0, sd=PPV_PCA0)
      ETA_EMAX ~ Normal( mean=0, sd=PPV_EMAX)
      ETA_C50 ~ Normal( mean=0, sd=PPV_C50)
      ETA_TEQ ~ Normal( mean=0, sd=PPV_TEQ)
   }# end RANDOM_VARIABLE_DEFINITION

   INDIVIDUAL_VARIABLES{
# The INDIVIDUAL_VARIABLES block is used to express the individual variables and their distribution.
# If this is not a population model then this block is not required.

	 ln(CL) = linear( trans is ln, pop = POP_CL, fixEff = [{coeff=BETA_CL_WT, cov=logtWT}] , ranEff = [ETA_CL] )
	 ln(V) = linear( trans is ln, pop = POP_V, fixEff = [{coeff=BETA_V_WT, cov=logtWT}] , ranEff = [ETA_V] )
     ln(KA) = linear( trans is ln, pop = POP_KA, ranEff = [ETA_KA] )
	 ln(TLAG) = linear( trans is ln, pop = POP_TLAG, ranEff = [ETA_TLAG] )

	  ln(PCA0) = linear( trans is ln, pop = POP_PCA0, ranEff = [ETA_PCA0] )
	  EMAX = linear(pop = POP_EMAX, ranEff = [ETA_EMAX] )
	  ln(C50) = linear( trans is ln, pop = POP_C50, ranEff = [ETA_C50] )
	  ln(TEQ) = linear( trans is ln, pop = POP_TEQ, ranEff = [ETA_TEQ] )   
   }# end INDIVIDUAL_VARIABLES

   MODEL_PREDICTION{
   	  KPCA=ln(2)/TEQ
      RPCA=PCA0*KPCA
   DEQ{
      RATEIN= if(T >= TLAG) then KA*GUT
			  else 0
	  GUT : { deriv =(- RATEIN), init = 0, x0 = 0 }
	  CENTRAL : { deriv =(RATEIN - CL * CENTRAL / V), init = 0, x0 = 0 } 
      CC=CENTRAL/V

      DPCA=PCA
      PD=1-EMAX*CC/(C50+CC)			
      PCA : {deriv=RPCA*PD - KPCA*DPCA, init = PCA0}
   	  }# end ODE
   }# end MODEL_PREDICTION

	RANDOM_VARIABLE_DEFINITION(level=DV){
      EPS_CP ~ Normal( mean=0, var=1)
      EPS_PCA ~ Normal( mean=0, var=1)
	}

   OBSERVATION{
   	 CP_obs = combinedError1( additive = RUV_ADD, proportional = RUV_PROP, eps = EPS_CP, prediction = CC ) 
   
   	 PCA_obs = additiveError( additive = RUV_FX, eps = EPS_PCA, prediction = PCA )
   }# end OBSERVATION
} # end of model object

warfarin_PKPD_turnover_task = taskObj {
ESTIMATE{ 
		set algo is saem
}
} # end of task object

warfarin_PK_ODE_mog = mogObj {
    OBJECTS{
		warfarin_PKPD_turnover_dat : { type is dataObj }
		warfarin_PKPD_turnover_mdl : { type is mdlObj }
		warfarin_PKPD_turnover_par : { type is parObj }
		warfarin_PKPD_turnover_task : { type is taskObj }
	}
}
